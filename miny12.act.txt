;MINY - ACTION VERZE 0.12
;
;ZACATEK PROGRAMU:
;
CHAR ARRAY REZIM="NE "
;
BYTE MINA=[84]
BYTE ZNMINY=[10]
BYTE VYSKA=[15]
BYTE SIRKA=[10]
BYTE ZACX
BYTE ZACY
BYTE POC,KEYC
BYTE KX,KY,CX,CY
BYTE ZBYTEK,SKZBY
BYTE KURZ=752,LEVY=82
;
CARD VIDEO=88,POLE
CARD POCMIN=[15],MAXMIN
;
INCLUDE "D8:TIINDATA.ACT"
;
PROC CLEDIAL(BYTE RADEK)
 POSITION(0,RADEK)
 PRINT("                                      ")
RETURN
;
PROC CISTIPOLE()
;NULOVANI POLE
 ZERO(POLE,960)
RETURN
;
CARD FUNC KAMDOPOLE(BYTE X,Y)
 RETURN(POLE+Y*40+X)
;
CARD FUNC KAMVIDEO(BYTE X,Y)
 RETURN(VIDEO+Y*40+X)
;
BYTE FUNC DATA(BYTE MX,MY)
 BYTE POINTER POM
 POM=KAMDOPOLE(MX,MY)
RETURN(POM^)
;
PROC TISK(BYTE ZNAK,X,Y)
 BYTE POINTER KDE
 KDE=KAMVIDEO(X,Y)
 KDE^=ZNAK
RETURN
;
PROC INVTISK(BYTE X,Y)
 BYTE POINTER KDE
 BYTE ZNAK
 KDE=KAMVIDEO(X,Y)
 ZNAK=KDE^
 IF ZNAK<128 THEN
  ZNAK==+128
 ELSE
  ZNAK==-128
 FI
 KDE^=ZNAK
RETURN
;
PROC ROZMISTI()
 BYTE X,Y,I,VYSKA1,SIRKA1,POM,OBSAH
 INT VYSL
 BYTE POINTER KDE
 CLEDIAL(2)
 POSITION(11,2)
 PRINT("═розмистуйи═мины═")
 CISTIPOLE()
 VYSL=SCOMPARE(REZIM,"ANO")
 IF VYSL<>0 THEN
  POM=1
  VYSKA1=VYSKA-2
  SIRKA1=SIRKA-2
 ELSE
  POM=0
  VYSKA1=VYSKA
  SIRKA1=SIRKA
 FI
 FOR I=1 TO POCMIN
  DO
   DO
    X=RAND(SIRKA1)+POM
    Y=RAND(VYSKA1)+POM
    KDE=KAMDOPOLE(X,Y)
    OBSAH=KDE^
    UNTIL OBSAH=0
   OD
   KDE^=MINA
  OD
RETURN
;
PROC INITPOLE()
 ZACY=((20-VYSKA)/2)+4
 ZACX=((40-SIRKA)/2)
 ZBYTEK=POCMIN
 SKZBY=POCMIN
RETURN
;
PROC POLEBEZ()
 BYTE X,Y
 PRINT("}")
 FOR Y=ZACY TO ZACY+VYSKA-1
  DO
   X=ZACX-1
   TISK(124,X,Y)
   X=ZACX+SIRKA
   TISK(124,X,Y)
  OD
 FOR X=ZACX TO ZACX+SIRKA-1
  DO
   Y=ZACY-1
   TISK(82,X,Y)
   Y=ZACY+VYSKA
   TISK(82,X,Y)
  OD
 TISK(81,ZACX-1,ZACY-1)
 TISK(90,ZACX-1,ZACY+VYSKA)
 TISK(69,ZACX+SIRKA,ZACY-1)
 TISK(67,ZACX+SIRKA,ZACY+VYSKA)
 FOR Y=ZACY TO (ZACY+VYSKA-1)
  DO
   FOR X=ZACX TO (ZACX+SIRKA-1)
    DO
     TISK(128,X,Y)
    OD
  OD
 KX=0
 KY=0
 INVTISK(KX+ZACX,KY+ZACY)
RETURN
;
PROC VYBUCH()
 BYTE SPATNE=[56]
 BYTE X,Y,KODP,KODV,PX,PY,POMY,POMX
 BYTE POINTER KDE,VPOLI  
 POMX=SIRKA-1
 POMY=VYSKA-1
 FOR Y=0 TO POMY
  DO
   PY=ZACY+Y
   FOR X=0 TO POMX
    DO
     PX=ZACX+X
     VPOLI=KAMDOPOLE(X,Y)
     KDE=KAMVIDEO(PX,PY)
     KODP=VPOLI^
     KODV=KDE^
     IF KODP=MINA AND KODV<>ZNMINY THEN
      TISK(MINA,PX,PY)
     FI
     IF KODV=ZNMINY AND KODP<>MINA THEN
      TISK(SPATNE,PX,PY)
     FI
   OD
  OD
RETURN
;
PROC POLICKO(BYTE RX,RY)
 BYTE POM
 BYTE POINTER WHERE
 WHERE=KAMDOPOLE(RX,RY)
 POM=WHERE^
 IF POM=MINA THEN
  POC==+1
 FI
RETURN
;
PROC SLOUPEC(BYTE QX,QY)
 BYTE AY
 IF QY>0 THEN
  AY=QY-1
  POLICKO(QX,AY)
 FI
 POLICKO(QX,QY)
 IF QY<VYSKA-1 THEN
  AY=QY+1
  POLICKO(QX,AY)
 FI
RETURN
;
PROC KOSTKA(BYTE X,Y)
 BYTE XX 
 IF X>0 THEN
  XX=X-1
  SLOUPEC(XX,Y)
 FI
  SLOUPEC(X,Y)
 IF X<SIRKA-1 THEN
  XX=X+1
  SLOUPEC(XX,Y)
 FI
RETURN
;
PROC PROHLEDEJ()
 BYTE X,Y,POMX,POMY,POM1,POM3
 BYTE POINTER KDE
 CLEDIAL(2)
 POSITION(7,2)
 PRINT("═прохледажам═миноже═поле═")
 POMX=SIRKA-1
 POMY=VYSKA-1
 FOR Y=0 TO POMY
  DO
   FOR X=0 TO POMX
    DO
     KDE=KAMDOPOLE(X,Y)
     POM1=KDE^
     IF POM1<>MINA THEN
      POC=0
      KOSTKA(X,Y)
      IF POC<>0 THEN
       KDE^=POC+16
      FI
     FI
    OD
  OD  
 CLEDIAL(2)
RETURN
;
PROC URCENI(BYTE KEYB)
 CX=KX
 CY=KY
 IF KEYB=42 AND KX<(SIRKA-1) THEN KX==+1 FI
 IF KEYB=43 AND KX>0 THEN KX==-1 FI
 IF KEYB=45 AND KY>0 THEN KY==-1 FI
 IF KEYB=61 AND KY<(VYSKA-1) THEN KY==+1 FI
RETURN
;
PROC POHYB()
 INVTISK(CX+ZACX,CY+ZACY)
 INVTISK(KX+ZACX,KY+ZACY)
RETURN
;
PROC VODOR(BYTE Y)
 BYTE X,POM
 X=KX
 IF X<>KX OR Y<>KY THEN
  POM=DATA(X,Y)
  IF POM=0 THEN
   TISK(14,X+ZACX,Y+ZACY)
  ELSE
   TISK(POM,X+ZACX,Y+ZACY)
  FI
 FI
 WHILE X>0
  DO
   X==-1
   POM=DATA(X,Y)
   IF POM<>MINA THEN
    IF POM=0 THEN
     TISK(14,X+ZACX,Y+ZACY)
    ELSE
     TISK(POM,X+ZACX,Y+ZACY)
     EXIT
    FI
   ELSE
    EXIT
   FI
  OD
 X=KX
 WHILE X<SIRKA-1
  DO
   X==+1
   POM=DATA(X,Y)
   IF POM<>MINA THEN
    IF POM=0 THEN
     TISK(14,X+ZACX,Y+ZACY)
    ELSE
     TISK(POM,X+ZACX,Y+ZACY)
     EXIT
    FI
   ELSE
    EXIT
   FI
  OD
RETURN
;
PROC DALSI()
 BYTE POM1,Y
 Y=KY
 VODOR(Y)
 WHILE Y>0 AND DATA(KX,Y)=0
  DO
   Y==-1
   VODOR(Y)
  OD
 Y=KY
 WHILE Y<(VYSKA-1) AND DATA(KX,Y)=0
  DO
   Y==+1
   VODOR(Y)
  OD
RETURN     
;
PROC ODKRYJ()
 BYTE POM
 BYTE POINTER VPOLI
 VPOLI=KAMDOPOLE(KX,KY)
 POM=VPOLI^
 IF POM=MINA THEN
  VYBUCH()
  KEYC=27
 ELSE  
  IF POM=0 THEN
   TISK(142,KX+ZACX,KY+ZACY)
   DALSI()
  ELSE
   TISK(POM+128,KX+ZACX,KY+ZACY)
  FI
 FI
RETURN
;
PROC OZNAC()
 BYTE POINTER KDEV
 KDEV=KAMDOPOLE(KX,KY)
 TISK(138,KX+ZACX,KY+ZACY)
 IF ZBYTEK>0 THEN ZBYTEK==-1 FI
 IF SKZBY>0 AND KDEV^=MINA THEN
  SKZBY==-1
 FI
RETURN
;
PROC PRUBEH()
 POSITION(0,0) PRINT("Zbyva najit ")
 PRINTB(ZBYTEK) PRINT(" min!  ")
 IF SKZBY=0 THEN
  CLEDIAL(0)
  POSITION(0,0) PRINT("VSECHNY MINY NALEZENY!")
 FI
RETURN
;
PROC HRA()
 INT KUKU
 INITPOLE()
 POLEBEZ()
 ROZMISTI()
 PROHLEDEJ()
 KUKU=SCOMPARE(REZIM,"ANO")
 IF KUKU=0 THEN
  CLEDIAL(2)
  POSITION(6,2)
  PRINT("═позор║═мины═и═на═окрайицх║═")
 FI
 DO
  PRUBEH()
  IF SKZBY=0 THEN EXIT FI
  KEYC=GETD(1)
  IF KEYC=32 THEN ODKRYJ() FI
  IF KEYC=155 THEN OZNAC() FI 
  IF KEYC=42 OR KEYC=43 OR KEYC=45 OR KEYC=61 THEN
   URCENI(KEYC)
   POHYB()
  FI
  UNTIL KEYC=27
 OD
 INVTISK(KX+ZACX,KY+ZACY)
 CLEDIAL(2)
 POSITION(12,2)
 PRINT("═стлац═клажесу═")
 KEYC=GETD(1)
RETURN
;
CARD FUNC MAX(BYTE V,S)
 INT VYSL
 CARD POM
 VYSL=SCOMPARE(REZIM,"ANO")
 IF VYSL<>0 THEN
  V==-2
  S==-2
 FI
 POM=V*S
RETURN(POM/4)
;
PROC ZMENAREZ()
 INT POM    
 POM=SCOMPARE(REZIM,"ANO")
 IF POM=0 THEN
  REZIM="NE "
 ELSE
  REZIM="ANO"
 FI
RETURN
;
PROC TISKPOC()
 MAXMIN=MAX(VYSKA,SIRKA)
 IF POCMIN>MAXMIN THEN POCMIN=MAXMIN FI
 POSITION(7,13)
 PRINT("POCET MIN (MAX. ")
 PRINTC(MAXMIN)
 PRINT("):  ")
 POSITION(30,13)
 PRINTC(POCMIN)
 PRINT("  ")
RETURN
;
PROC PARAMTISK()
 BYTE KEY
 PRINT("}")
 POSITION(9,3)
 PRINT("V Y B E R T E   S I :")
 POSITION(9,4)
 PRINT("")
 POSITION(7,8)
 PRINT("INSTRUKCE   (ретурн)")
 POSITION(7,10)
 PRINT("VYSKA (5 az 18) :      ") PRINTB(VYSKA)
 POSITION(7,11)
 PRINT("SIRKA (5 AZ 38) :      ") PRINTB(SIRKA)
 POSITION(7,12)
 PRINT("MINY I V OKRAJI?:      ") PRINT(REZIM)
 TISKPOC()
 POSITION(7,16)
 PRINT("SPUSTIT HRU (ретурн)")
 POSITION(7,17)
 PRINT("KONEC HRY   (ретурн)")
 POSITION(7,20)
 PRINT("═узижейте═сипкы╨═²═°═·═÷═")
RETURN
;
PROC TITUL()
 BYTE KONSOLA=53279 
 CARD ADRESA
 ADRESA=KAMVIDEO(0,0)
 MOVEBLOCK(ADRESA,TITULDAT,960)
 WHILE KONSOLA=7
  DO
  OD
RETURN
;
PROC INSTRUKCE()
 BYTE KONSOLA=53279
 CARD ADRESA
 ADRESA=KAMVIDEO(0,0)
 MOVEBLOCK(ADRESA,INSTRDAT,960)
 WHILE KONSOLA=7
  DO
  OD
RETURN
;
PROC TODOS()
 [$6C $0A $00]
RETURN
;
PROC MENU()
 BYTE KEY,TX,TY,TXX,TYY
 POLE=TITULDAT
 GRAPHICS(0)
 SETCOLOR(2,0,0)
 KURZ=1
 LEVY=1
 TITUL()
 CLOSE(1)
 OPEN(1,"K:",4,0)
 PARAMTISK()
 TX=6 TY=8 TYY=8
 POSITION(TX,TY) PRINT("")
 DO
  KEY=GETD(1)
  IF KEY=61 AND TY<17 THEN
   POSITION(TX,TY) PRINT (" ")
   TY==+1
   IF TY=9 THEN TY=10 FI
   IF TY=14 THEN TY=16 FI
   POSITION(TX,TY) PRINT("")
  FI
  IF KEY=45 AND TY>8 THEN
   POSITION(TX,TY) PRINT(" ")
   TY==-1
   IF TY=15 THEN TY=13 FI
   IF TY=9 THEN TY=8 FI
   POSITION(TX,TY) PRINT("")
  FI
  IF KEY=43 THEN
   IF TY=10 AND VYSKA>5 THEN
    VYSKA==-1
    POSITION(30,TY) PRINTB(VYSKA) PRINT(" ")
    TISKPOC()
   FI
   IF TY=11 AND SIRKA>5 THEN
    SIRKA==-1
    POSITION(30,TY) PRINTB(SIRKA) PRINT("  ")
    TISKPOC()
   FI
   IF TY=12 THEN
    ZMENAREZ()
    POSITION(30,TY) PRINT(REZIM)
    TISKPOC()
   FI
   IF TY=13 AND POCMIN>2 THEN
    POCMIN==-1
    POSITION(30,TY)
    PRINTC(POCMIN) PRINT("  ")
   FI
  FI
  IF KEY=42 THEN
   IF TY=10 AND VYSKA<18 THEN
    VYSKA==+1
    POSITION(30,TY)
    PRINTB(VYSKA) PRINT("  ")
    TISKPOC()
   FI
   IF TY=11 AND SIRKA<38 THEN
    SIRKA==+1
    POSITION(30,TY)
    PRINTB(SIRKA) PRINT("  ")
    TISKPOC()
   FI
   IF TY=12 THEN
    ZMENAREZ()
    POSITION(30,TY)
    PRINT(REZIM)
    TISKPOC()
   FI
   IF TY=13 AND POCMIN<MAXMIN THEN
    POCMIN==+1
    POSITION(30,TY)
    PRINTC(POCMIN) PRINT("  ")
   FI
  FI
  IF KEY=155 AND TY=8 THEN
   INSTRUKCE()
   PARAMTISK()
   POSITION(TX,TY) PRINT("")
  FI
  IF KEY=155 AND TY=16 THEN
   HRA()
   PARAMTISK()
   POSITION(TX,TY) PRINT("")
  FI
  IF KEY=155 AND TY=17 THEN     
   EXIT
  FI
 OD
 CLOSE(1)
 GRAPHICS(0)
 TODOS()
RETURN
;
 
